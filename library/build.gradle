apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.3"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 22
        versionCode 1
        versionName "1.0.0"
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'consumer-proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
}

//region jitpack
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.ikecin.sdk'

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    options {
        encoding 'utf-8'
        charSet 'utf-8'
    }
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
//endregion

// region 发布到本地maven仓库
uploadArchives {
    println '------------'
    println rootDir.toPath().resolve('maven').toUri()
    println '------------'
    repositories {
        mavenDeployer {
            repository(url: rootDir.toPath().resolve('maven').toUri())
            pom.version = android.defaultConfig.versionName
            pom.artifactId = "device"
        }
    }
}
// endregion